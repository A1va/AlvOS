set $lastcs = -1
set $amd64 = -1

define hook-stop
  # 似乎没有很好的方法来检测我们是在16位还是32位模式下，但是我们总是在32位模式下运行CS == 8

  if $amd64 == -1
    if $cs == 8 || $cs == 27
      if $lastcs != 8 && $lastcs != 27 && $cs==8
        set architecture i386
      end
      x/i $pc
    else
      if $lastcs == -1 || $lastcs == 8 || $lastcs == 27 || $cs==0
        set architecture i8086
      end
      # 将 segment:offset 转换为物理地址
      printf "[%4x:%4x] ", $cs, $eip
      x/i $cs*16+$eip
    end
    set $lastcs = $cs
  end
end

# 初始化设置计算机体系架构为 x86_64
echo + target remote localhost:1234\n
target remote localhost:1234

# 如果失败，可能是电脑的 GDB 不支持 ELF.
echo + symbol-file obj/kern/kernel\n
symbol-file obj/kern/kernel

break jumpto_longmode
commands
  set $amd64 = 1
  tbreak *0x0
  commands
    set architecture i386:x86-64
    set $lastcs = $cs
    set $amd64 = 0
    continue
  end
  si
end
